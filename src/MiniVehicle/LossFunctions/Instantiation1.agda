
module MiniVehicle.LossFunctions.Instantiation1 where

open import Data.Sum as Sum
open import Data.Product as Prod
open import Data.Rational hiding (pos; neg)
open import Data.Rational.Properties
open import Data.Bool hiding (_â‰¤_; _<_; _<?_; _â‰¤?_) renaming (Bool to ğ”¹; T to True)
open import Data.Bool.Properties hiding (_<?_; _â‰¤?_)
open import Data.Integer using (+_)
open import Data.Empty using (âŠ¥-elim)
open import Algebra
open import Function
open import Function.Reasoning
open import Relation.Nullary hiding (True)
open import Relation.Binary.PropositionalEquality using (_â‰¡_; cong; refl)
open import Relation.Unary using (Decidable)
open import Relation.Nullary.Negation

open import Util
open import Util.PositiveRational
open import MiniVehicle.Language.StandardSemantics
open import MiniVehicle.LossFunctions.GenericDifferentiableLogic

open DifferentiableLogic

------------------------------------------------------------------------------
-- Define the signed non-negative rationals.

data Â±â„š : Set where
  pos : (p : â„šâº) â†’ Â±â„š
  neg : (p : â„šâº) â†’ Â±â„š

_âŸ¦andâŸ§_ : Â±â„š â†’ Â±â„š â†’ Â±â„š
pos x âŸ¦andâŸ§ pos y = pos (maxâº x y)
pos x âŸ¦andâŸ§ neg y = neg y
neg x âŸ¦andâŸ§ pos y = neg x
neg x âŸ¦andâŸ§ neg y = neg (minâº x y)

_âŸ¦orâŸ§_ : Â±â„š â†’ Â±â„š â†’ Â±â„š
pos x âŸ¦orâŸ§ pos y = pos (minâº x y)
pos x âŸ¦orâŸ§ neg y = pos y
neg x âŸ¦orâŸ§ pos y = pos x
neg x âŸ¦orâŸ§ neg y = neg (maxâº x y)

âŸ¦notâŸ§_ : Â±â„š â†’ Â±â„š
âŸ¦notâŸ§ pos x = neg x
âŸ¦notâŸ§ neg x = pos x

_âŸ¦â‰¤âŸ§_ : â„š â†’ â„š â†’ Â±â„š
x âŸ¦â‰¤âŸ§ y with x â‰¤? y
... | yes xâ‰¤y = pos (y - x , nonNegative (pâ‰¥qâ‡’p-qâ‰¥0 xâ‰¤y))
... | no  xâ‰°y = neg (x - y , nonNegative (pâ‰¥qâ‡’p-qâ‰¥0 (â‰°â‡’â‰¥ xâ‰°y)))

_âŸ¦<âŸ§_ : â„š â†’ â„š â†’ Â±â„š
x âŸ¦<âŸ§ y with x <? y
... | yes x<y = pos (y - x , nonNegative (pâ‰¥qâ‡’p-qâ‰¥0 (<â‡’â‰¤ x<y)))
... | no  xâ‰®y = neg (x - y , nonNegative (pâ‰¥qâ‡’p-qâ‰¥0 (â‰®â‡’â‰¥ xâ‰®y)))

logic : DifferentiableLogic
logic .âŸªBoolâŸ« = Â±â„š
logic ._âŸªandâŸ«_ = _âŸ¦andâŸ§_
logic ._âŸªorâŸ«_ = _âŸ¦orâŸ§_
logic .âŸªnotâŸ« = âŸ¦notâŸ§_
logic ._âŸªâ‰¤âŸ«_ = _âŸ¦â‰¤âŸ§_
logic ._âŸª<âŸ«_ = _âŸ¦<âŸ§_

------------------------------------------------------------------------------
-- Correctness

private
  variable
    a b : ğ”¹
    p q r : Â±â„š

data Truish : Â±â„š â†’ Set where
  truth : âˆ€ q â†’ Truish (pos q)

Truish? : Decidable Truish
Truish? (pos p) = yes (truth p)
Truish? (neg p) = no Î»()

âŸªandâŸ«-â‡¿ : âˆ€ p q â†’ (Truish p Ã— Truish q) â‡” (Truish (p âŸ¦andâŸ§ q))
âŸªandâŸ«-â‡¿ (pos p) (pos q) = mkâ‡” (Î» _ â†’ truth (maxâº p q)) (Î» _ â†’ truth p , truth q)
âŸªandâŸ«-â‡¿ (pos p) (neg q) = mkâ‡” (Î»()) (Î»())
âŸªandâŸ«-â‡¿ (neg p) (pos q) = mkâ‡” (Î»()) (Î»())
âŸªandâŸ«-â‡¿ (neg p) (neg q) = mkâ‡” (Î»()) (Î»())

âŸªorâŸ«-â‡¿ : âˆ€ p q â†’ (Truish p âŠ Truish q) â‡” (Truish (p âŸ¦orâŸ§ q))
âŸªorâŸ«-â‡¿ (pos p) (pos q) = mkâ‡” (Î» _ â†’ truth (minâº p q)) (Î» _ â†’ injâ‚ (truth p))
âŸªorâŸ«-â‡¿ (pos p) (neg q) = mkâ‡” (Î» _ â†’ truth q) (Î» _ â†’ injâ‚ (truth p))
âŸªorâŸ«-â‡¿ (neg p) (pos q) = mkâ‡” (Î» _ â†’ truth p) (Î» _ â†’ injâ‚‚ (truth q))
âŸªorâŸ«-â‡¿ (neg p) (neg q) = mkâ‡” (Î» {(injâ‚ ()); (injâ‚‚ ())}) (Î»())

âŸªnotâŸ«-â‡¿ : âˆ€ p â†’ Truish p â‡” (Â¬ (Truish (âŸ¦notâŸ§ p)))
âŸªnotâŸ«-â‡¿ (pos p)   = mkâ‡” (Î» _ ()) (Î» _ â†’ truth p)
âŸªnotâŸ«-â‡¿ (neg p) = mkâ‡” (Î»()) (Î» f â†’ âŠ¥-elim (f (truth p)))

âŸª<âŸ«-â‡¿ : âˆ€ p q â†’ True (p <áµ‡ q) â‡” Truish (p âŸ¦<âŸ§ q)
âŸª<âŸ«-â‡¿ p q with p <? q
... | yes p<q = mkâ‡” (Î» p<áµ‡q â†’ truth _) (Î» _ â†’ <â‡’<áµ‡ p<q)
... | no  pâ‰®q = mkâ‡” (Î» p<áµ‡q â†’ âŠ¥-elim (pâ‰®q (<áµ‡â‡’< p<áµ‡q))) Î»()

âŸªâ‰¤âŸ«-â‡¿ : âˆ€ p q â†’ True (p â‰¤áµ‡ q) â‡” Truish (p âŸ¦â‰¤âŸ§ q)
âŸªâ‰¤âŸ«-â‡¿ p q with p â‰¤? q
... | yes pâ‰¤q = mkâ‡” (Î» pâ‰¤áµ‡q â†’ truth _) (Î» _ â†’ â‰¤â‡’â‰¤áµ‡ pâ‰¤q)
... | no  pâ‰°q = mkâ‡” (Î» pâ‰¤áµ‡q â†’ âŠ¥-elim (pâ‰°q (â‰¤áµ‡â‡’â‰¤ pâ‰¤áµ‡q))) Î»()

valid : DifferentiableLogicRelation logic
valid = predicateToRelation logic $ record
  { Truish = Truish
  ; Truish? = Truish?
  ; âŸªandâŸ«-â‡¿ = âŸªandâŸ«-â‡¿
  ; âŸªorâŸ«-â‡¿ = âŸªorâŸ«-â‡¿
  ; âŸªnotâŸ«-â‡¿ = âŸªnotâŸ«-â‡¿
  ; âŸª<âŸ«-â‡¿ = âŸª<âŸ«-â‡¿
  ; âŸªâ‰¤âŸ«-â‡¿ = âŸªâ‰¤âŸ«-â‡¿
  }

------------------------------------------------------------------------------
-- Compilation

module _ (extFunc : â„š â†’ â„š) where

  open import MiniVehicle.LossFunctions.GenericCompilation
    using (lossRestriction)
  open import MiniVehicle.Language.Syntax lossRestriction

  open import MiniVehicle.Language.Syntax.Restriction

  open import MiniVehicle.LossFunctions.GenericCorrectness extFunc logic valid as L
  open Equivalence
  
  prec : â„š
  prec = + 1 / 10000000

  -- The calculation of the loss of any term
  loss : Îµ / Îµ âŠ¢ Bool (BoolRes U) â†’ â„š
  loss t with L.lossFunctionProp t
  ... | pos _ = 0â„š
  ... | neg (l , _) = l + prec

  -- Correspondance with standard semantics
  trueâ‡’lossâ‰¡0 : âˆ€ t â†’ True (standardProp t) â†’ loss t â‰¡ 0â„š
  trueâ‡’lossâ‰¡0 t tr with L.lossFunctionProp t | to (prop-correctness t) tr
  ... | pos p | x = refl

  falseâ‡’loss>0 : âˆ€ t â†’ Â¬ (True (standardProp t)) â†’ loss t > 0â„š
  falseâ‡’loss>0 t Â¬tr with L.lossFunctionProp t | from (prop-correctness t)
  ... | pos p | x = contradiction (x (truth p)) Â¬tr
  ... | neg (l , lâº) | x = positiveâ»Â¹ (l + prec) {{nonNegative+posâ‡’pos l prec {{lâº}}}}
