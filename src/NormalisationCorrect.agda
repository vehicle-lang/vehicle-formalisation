{-# OPTIONS --postfix-projections #-}

open import Level using (0â„“; suc; lift)

open import Data.Bool using (not; _âˆ§_; _âˆ¨_; true; false)
                   renaming (Bool to ğ”¹; T to True; if_then_else_ to ifáµ‡_then_else_)
open import Data.Bool.Properties using (âˆ¨-identityÊ³)
open import Data.Fin using (Fin)
open import Data.Nat using (â„•)
open import Data.Product using (_Ã—_; _,_; projâ‚; projâ‚‚; Î£-syntax)
open import Data.Rational using (â„š; _+_; _*_; _â‰¤áµ‡_; _â‰Ÿ_; 1â„š)
open import Data.Rational.Properties using (*-identityË¡; *-comm)
open import Data.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Data.Unit using (âŠ¤; tt)

open import Relation.Binary.PropositionalEquality
  using (_â‰¡_; refl; trans; cong; sym; congâ‚‚; subst; module â‰¡-Reasoning)

open import Util
open import MiniVehicle hiding (_â‡’áµ£_; under)
open import NormalisedExpr
open import Interpretation
open import Isomorphism

module NormalisationCorrect (extFunc : â„š â†’ â„š) where

  import StandardSemantics as S
  import Normalisation as N

  open Evaluation extFunc

  record World : Set where
    field
      ctxt : LinVarCtxt
      env  : Env ctxt
  open World public

  empty : World
  empty .ctxt = Îµ
  empty .env = empty-env

  -- World morphisms extend the context whilst making sure that the
  -- environment is preserved.
  record _â‡’w_ (wâ‚ wâ‚‚ : World) : Set where
    field
      ren   : wâ‚ .ctxt â‡’áµ£ wâ‚‚ .ctxt
      presv : âˆ€ x â†’ wâ‚ .env (ren x) â‰¡ wâ‚‚ .env x
  open _â‡’w_ public

  id-w : âˆ€ {w} â†’ w â‡’w w
  id-w .ren x = x
  id-w .presv x = refl

  _âˆ˜w_ : âˆ€ {wâ‚ wâ‚‚ wâ‚ƒ} â†’ wâ‚‚ â‡’w wâ‚ƒ â†’ wâ‚ â‡’w wâ‚‚ â†’ wâ‚ â‡’w wâ‚ƒ
  (f âˆ˜w g) .ren x = g .ren (f .ren x)
  (f âˆ˜w g) .presv x = trans (g .presv (f .ren x)) (f .presv x)

  extend-w : World â†’ â„š â†’ World
  extend-w w q .ctxt = w .ctxt ,âˆ™
  extend-w w q .env = extend-env (w .env) q

  under-w : âˆ€ {wâ‚ wâ‚‚ q} â†’ (wâ‚ â‡’w wâ‚‚) â†’ (extend-w wâ‚ q â‡’w extend-w wâ‚‚ q)
  under-w Ï .ren = under (Ï .ren)
  under-w Ï .presv zero = refl
  under-w Ï .presv (succ x) = Ï .presv x

  under-w' : âˆ€ {wâ‚ wâ‚‚ qâ‚ qâ‚‚} â†’ (qâ‚ â‰¡ qâ‚‚) â†’ (wâ‚ â‡’w wâ‚‚) â†’ (extend-w wâ‚ qâ‚ â‡’w extend-w wâ‚‚ qâ‚‚)
  under-w' eq Ï .ren = under (Ï .ren)
  under-w' eq Ï .presv zero = eq
  under-w' eq Ï .presv (succ x) = Ï .presv x

  wk-w : âˆ€ {w q} â†’ extend-w w q â‡’w w
  wk-w .ren = succ
  wk-w .presv x = refl

  ------------------------------------------------------------------------------
  -- Our category of related interpretations

  -- module ğ’® = Model (S.â„³ extFunc)
  module ğ’© = Model N.â„³
  module ğ’® = Model (S.â„³ extFunc)

  record WRel : Set (suc 0â„“) where
    field
      Left  : ğ’®.âŸ¦TypeâŸ§
      Right : ğ’©.âŸ¦TypeâŸ§
      rel   : (w : World) â†’ Left â†’ Right .N.Carrier (w .ctxt) â†’ Set
      ext   : âˆ€ {w w'} (Ï : w' â‡’w w) a b â†’ rel w a b â†’ rel w' a (Right .N.rename (Ï .ren) b)
  open WRel

  record _===>_ (X Y : WRel) : Set where
    field
      left    : X .Left â†’ Y .Left
      right   : X .Right N.==> Y .Right
      rel-mor : âˆ€ w lx rx â†’ X .rel w lx rx â†’ Y .rel w (left lx) (right .N.mor rx)
  open _===>_

  ------------------------------------------------------------------------------
  -- Composition

  _âˆ˜R_ : âˆ€ {X Y Z} â†’ (Y ===> Z) â†’ (X ===> Y) â†’ (X ===> Z)
  (f âˆ˜R g) .left x = f .left (g .left x)
  (f âˆ˜R g) .right = f .right ğ’©.âˆ˜ g .right
  (f âˆ˜R g) .rel-mor w xâ‚ xâ‚‚ r-xâ‚xâ‚‚ = f .rel-mor w _ _ (g .rel-mor w _ _ r-xâ‚xâ‚‚)

  âŸ¦idâŸ§R : âˆ€ {X} â†’ X ===> X
  âŸ¦idâŸ§R .left x = x
  âŸ¦idâŸ§R .right = ğ’©.âŸ¦idâŸ§
  âŸ¦idâŸ§R .rel-mor w xâ‚ xâ‚‚ r = r

  ------------------------------------------------------------------------------
  -- Sets
  FlatR : Set â†’ WRel
  FlatR X .Left = X
  FlatR X .Right = N.K X
  FlatR X .rel w xâ‚ xâ‚‚ = xâ‚ â‰¡ xâ‚‚
  FlatR X .ext Ï xâ‚ xâ‚‚ eq = eq

  ------------------------------------------------------------------------------
  -- Products and terminal object
  âŸ¦âŠ¤âŸ§R : WRel
  âŸ¦âŠ¤âŸ§R .Left = âŠ¤
  âŸ¦âŠ¤âŸ§R .Right = ğ’©.âŸ¦âŠ¤âŸ§
  âŸ¦âŠ¤âŸ§R .rel w tt tt = âŠ¤
  âŸ¦âŠ¤âŸ§R .ext Ï tt tt x = x

  âŸ¦terminalâŸ§R : âˆ€ {X} â†’ X ===> âŸ¦âŠ¤âŸ§R
  âŸ¦terminalâŸ§R .left _ = tt
  âŸ¦terminalâŸ§R .right = ğ’©.âŸ¦terminalâŸ§
  âŸ¦terminalâŸ§R .rel-mor _ _ _ _ = tt

  _âŸ¦Ã—âŸ§R_ : WRel â†’ WRel â†’ WRel
  (X âŸ¦Ã—âŸ§R Y) .Left = X .Left Ã— Y .Left
  (X âŸ¦Ã—âŸ§R Y) .Right = X .Right ğ’©.âŸ¦Ã—âŸ§ Y .Right
  (X âŸ¦Ã—âŸ§R Y) .rel w (x , y) (x' , y') = X .rel w x x' Ã— Y .rel w y y'
  (X âŸ¦Ã—âŸ§R Y) .ext Ï (x , y) (x' , y') (râ‚ , râ‚‚) =
    (X .ext Ï x x' râ‚) , (Y .ext Ï y y' râ‚‚)

  âŸ¨_,_âŸ©R : âˆ€ {X Y Z} â†’ (X ===> Y) â†’ (X ===> Z) â†’ (X ===> (Y âŸ¦Ã—âŸ§R Z))
  âŸ¨ f , g âŸ©R .left x = (f .left x) , (g .left x)
  âŸ¨ f , g âŸ©R .right = ğ’©.âŸ¨ f .right , g .right âŸ©
  âŸ¨ f , g âŸ©R .rel-mor w xâ‚ xâ‚‚ r-xâ‚xâ‚‚ =
    f .rel-mor w xâ‚ xâ‚‚ r-xâ‚xâ‚‚ ,
    g .rel-mor w xâ‚ xâ‚‚ r-xâ‚xâ‚‚

  âŸ¦projâ‚âŸ§R : âˆ€ {X Y} â†’ (X âŸ¦Ã—âŸ§R Y) ===> X
  âŸ¦projâ‚âŸ§R .left = projâ‚
  âŸ¦projâ‚âŸ§R .right = ğ’©.âŸ¦projâ‚âŸ§
  âŸ¦projâ‚âŸ§R .rel-mor w _ _ r = r .projâ‚

  âŸ¦projâ‚‚âŸ§R : âˆ€ {X Y} â†’ (X âŸ¦Ã—âŸ§R Y) ===> Y
  âŸ¦projâ‚‚âŸ§R .left = projâ‚‚
  âŸ¦projâ‚‚âŸ§R .right = ğ’©.âŸ¦projâ‚‚âŸ§
  âŸ¦projâ‚‚âŸ§R .rel-mor w _ _ r = r .projâ‚‚

  ------------------------------------------------------------------------------
  -- Functions and Universal Quantification

  _âŸ¦â‡’âŸ§R_ : WRel â†’ WRel â†’ WRel
  (X âŸ¦â‡’âŸ§R Y) .Left = X .Left â†’ Y .Left
  (X âŸ¦â‡’âŸ§R Y) .Right = X .Right ğ’©.âŸ¦â‡’âŸ§ Y .Right
  (X âŸ¦â‡’âŸ§R Y) .rel w f g =
    âˆ€ w' (Ï : w' â‡’w w) x y â†’
       X .rel w' x y â†’
       Y .rel w' (f x) (g (w' .ctxt) (Ï .ren) y)
  (X âŸ¦â‡’âŸ§R Y) .ext Ï f g r =
    Î» w'' Ï' x y â†’ r w'' (Ï âˆ˜w Ï') x y

  âŸ¦Î›âŸ§R : âˆ€ {X Y Z} â†’ ((X âŸ¦Ã—âŸ§R Y) ===> Z) â†’ (X ===> (Y âŸ¦â‡’âŸ§R Z))
  âŸ¦Î›âŸ§R {X} f .left x y = f .left (x , y)
  âŸ¦Î›âŸ§R {X} f .right = ğ’©.âŸ¦Î›âŸ§ (f .right)
  âŸ¦Î›âŸ§R {X} f .rel-mor w xâ‚ xâ‚‚ r-xâ‚xâ‚‚ w' Ï yâ‚ yâ‚‚ r-yâ‚yâ‚‚ =
    f .rel-mor w' (xâ‚ , yâ‚)
                  (X .Right .N.rename (Ï .ren) xâ‚‚ , yâ‚‚)
                  (X .ext Ï xâ‚ xâ‚‚ r-xâ‚xâ‚‚ , r-yâ‚yâ‚‚)

  âŸ¦evalâŸ§R : âˆ€ {X Y} â†’ ((X âŸ¦â‡’âŸ§R Y) âŸ¦Ã—âŸ§R X) ===> Y
  âŸ¦evalâŸ§R .left (f , x) = f x
  âŸ¦evalâŸ§R .right = ğ’©.âŸ¦evalâŸ§
  âŸ¦evalâŸ§R .rel-mor w (fâ‚ , xâ‚) (fâ‚‚ , xâ‚‚) (r-fâ‚fâ‚‚ , r-xâ‚xâ‚‚) =
    r-fâ‚fâ‚‚ w id-w xâ‚ xâ‚‚ r-xâ‚xâ‚‚

  âŸ¦âˆ€âŸ§R : âˆ€ {I : Set} â†’ (I â†’ WRel) â†’ WRel
  âŸ¦âˆ€âŸ§R A .Left = âˆ€ n â†’ A n .Left
  âŸ¦âˆ€âŸ§R A .Right = ğ’©.âŸ¦âˆ€âŸ§ (Î» n â†’ A n .Right)
  âŸ¦âˆ€âŸ§R A .rel w x y = âˆ€ n â†’ A n .rel w (x n) (y n)
  âŸ¦âˆ€âŸ§R A .ext Ï x y r n = A n .ext Ï (x n) (y n) (r n)

  âŸ¦âˆ€-introâŸ§R : âˆ€ {I X A} â†’ (âˆ€ (n : I) â†’ X ===> A n) â†’ X ===> âŸ¦âˆ€âŸ§R A
  âŸ¦âˆ€-introâŸ§R f .left x n = f n .left x
  âŸ¦âˆ€-introâŸ§R f .right = ğ’©.âŸ¦âˆ€-introâŸ§ (Î» n â†’ f n .right)
  âŸ¦âˆ€-introâŸ§R f .rel-mor w xâ‚ xâ‚‚ r n = f n .rel-mor w xâ‚ xâ‚‚ r

  âŸ¦âˆ€-elimâŸ§R : âˆ€ {I A} (n : I) â†’ âŸ¦âˆ€âŸ§R A ===> A n
  âŸ¦âˆ€-elimâŸ§R n .left f = f n
  âŸ¦âˆ€-elimâŸ§R n .right = ğ’©.âŸ¦âˆ€-elimâŸ§ n
  âŸ¦âˆ€-elimâŸ§R n .rel-mor w fâ‚ fâ‚‚ r = r n

  ------------------------------------------------------------------------------
  KR : Set â†’ WRel
  KR X .Left = X
  KR X .Right = N.K X
  KR X .rel w x y = x â‰¡ y
  KR X .ext Ï x y eq = eq

  âŸ¦IndexâŸ§R : â„• â†’ WRel
  âŸ¦IndexâŸ§R n = KR (Fin n)

  ext-evalLinExp :
    âˆ€ {wâ‚ wâ‚‚} e (Ï : wâ‚‚ â‡’w wâ‚) â†’
      eval-LinExp e (wâ‚ .env) â‰¡ eval-LinExp (rename-LinExp (Ï .ren) e) (wâ‚‚ .env)
  ext-evalLinExp (const q)   Ï = refl
  ext-evalLinExp (var q x)   Ï = cong (Î» â–¡ â†’ q * â–¡) (sym (Ï .presv x))
  ext-evalLinExp (eâ‚ `+` eâ‚‚) Ï = congâ‚‚ _+_ (ext-evalLinExp eâ‚ Ï) (ext-evalLinExp eâ‚‚ Ï)

  ext-evalConstraint :
    âˆ€ {wâ‚ wâ‚‚} p (Ï : wâ‚‚ â‡’w wâ‚) â†’
      eval-Constraint p (wâ‚ .env)
      â‰¡ eval-Constraint (rename-Constraint (Ï .ren) p) (wâ‚‚ .env)
  ext-evalConstraint (eâ‚ `â‰¤` eâ‚‚) Ï rewrite ext-evalLinExp eâ‚ Ï rewrite ext-evalLinExp eâ‚‚ Ï = refl
  ext-evalConstraint (eâ‚ `>` eâ‚‚) Ï rewrite ext-evalLinExp eâ‚ Ï rewrite ext-evalLinExp eâ‚‚ Ï = refl
  ext-evalConstraint (eâ‚ `=` eâ‚‚) Ï rewrite ext-evalLinExp eâ‚ Ï rewrite ext-evalLinExp eâ‚‚ Ï = refl
  ext-evalConstraint (eâ‚ `â‰ ` eâ‚‚) Ï rewrite ext-evalLinExp eâ‚ Ï rewrite ext-evalLinExp eâ‚‚ Ï = refl
  ext-evalConstraint (p and q)   Ï rewrite ext-evalConstraint p Ï rewrite ext-evalConstraint q Ï = refl
  ext-evalConstraint (p or q)    Ï rewrite ext-evalConstraint p Ï rewrite ext-evalConstraint q Ï = refl
  ext-evalConstraint (x `=`f y)  Ï rewrite Ï .presv x rewrite Ï .presv y = refl
  ext-evalConstraint (x `â‰ `f y)  Ï rewrite Ï .presv x rewrite Ï .presv y = refl

  ------------------------------------------------------------------------------
  -- Numbers, and linear expressions
  âŸ¦NumâŸ§R : LinearityVal â†’ WRel
  âŸ¦NumâŸ§R p .Left = ğ’®.âŸ¦NumâŸ§ p
  âŸ¦NumâŸ§R p .Right = ğ’©.âŸ¦NumâŸ§ p
  âŸ¦NumâŸ§R const .rel _ qâ‚ qâ‚‚ = qâ‚ â‰¡ qâ‚‚
  âŸ¦NumâŸ§R const .ext _ _ _ eq = eq
  âŸ¦NumâŸ§R linear .rel w x exp = x â‰¡ eval-LinExp exp (w .env)
  âŸ¦NumâŸ§R linear .ext Ï x exp eq = trans eq (ext-evalLinExp exp Ï)
  âŸ¦NumâŸ§R nonlinear .rel w x tt = âŠ¤
  âŸ¦NumâŸ§R nonlinear .ext _ _ _ _ = tt

  âŸ¦constâŸ§R : âˆ€ {X} â†’ â„š â†’ X ===> âŸ¦NumâŸ§R const
  âŸ¦constâŸ§R q .left _ = q
  âŸ¦constâŸ§R q .right = ğ’©.âŸ¦constâŸ§ q
  âŸ¦constâŸ§R q .rel-mor w _ _ _ = refl

  âŸ¦addâŸ§R : âˆ€ {lâ‚ lâ‚‚ lâ‚ƒ} â†’
           (FlatR (MaxLinRel lâ‚ lâ‚‚ lâ‚ƒ) âŸ¦Ã—âŸ§R (âŸ¦NumâŸ§R lâ‚ âŸ¦Ã—âŸ§R âŸ¦NumâŸ§R lâ‚‚)) ===> âŸ¦NumâŸ§R lâ‚ƒ
  âŸ¦addâŸ§R .left = ğ’®.âŸ¦addâŸ§
  âŸ¦addâŸ§R .right = ğ’©.âŸ¦addâŸ§
  âŸ¦addâŸ§R .rel-mor w (const-const   , xâ‚ , yâ‚) (_ , xâ‚‚ , yâ‚‚) (refl , xâ‚â‚‚ , yâ‚â‚‚) =
    congâ‚‚ _+_ xâ‚â‚‚ yâ‚â‚‚
  âŸ¦addâŸ§R .rel-mor w (const-linear  , xâ‚ , yâ‚) (_ , xâ‚‚ , yâ‚‚) (refl , xâ‚â‚‚ , yâ‚â‚‚) =
    congâ‚‚ _+_ xâ‚â‚‚ yâ‚â‚‚
  âŸ¦addâŸ§R .rel-mor w (linear-const  , xâ‚ , yâ‚) (_ , xâ‚‚ , yâ‚‚) (refl , xâ‚â‚‚ , yâ‚â‚‚) =
    congâ‚‚ _+_ xâ‚â‚‚ yâ‚â‚‚
  âŸ¦addâŸ§R .rel-mor w (linear-linear , xâ‚ , yâ‚) (_ , xâ‚‚ , yâ‚‚) (refl , xâ‚â‚‚ , yâ‚â‚‚) =
    congâ‚‚ _+_ xâ‚â‚‚ yâ‚â‚‚

  âŸ¦mulâŸ§R : âˆ€ {lâ‚ lâ‚‚ lâ‚ƒ} â†’ (FlatR (MulRel lâ‚ lâ‚‚ lâ‚ƒ) âŸ¦Ã—âŸ§R (âŸ¦NumâŸ§R lâ‚ âŸ¦Ã—âŸ§R âŸ¦NumâŸ§R lâ‚‚)) ===> âŸ¦NumâŸ§R lâ‚ƒ
  âŸ¦mulâŸ§R .left = ğ’®.âŸ¦mulâŸ§
  âŸ¦mulâŸ§R .right = ğ’©.âŸ¦mulâŸ§
  âŸ¦mulâŸ§R .rel-mor w (const-const  , xâ‚ , yâ‚) (_ , xâ‚‚ , yâ‚‚) (refl , xâ‚â‚‚ , yâ‚â‚‚) =
    congâ‚‚ _*_ xâ‚â‚‚ yâ‚â‚‚
  âŸ¦mulâŸ§R .rel-mor w (const-linear , xâ‚ , yâ‚) (_ , xâ‚‚ , yâ‚‚) (refl , xâ‚â‚‚ , yâ‚â‚‚) =
    trans (congâ‚‚ _*_ xâ‚â‚‚ yâ‚â‚‚) (eval-âŠ› xâ‚‚ yâ‚‚ (w .env))
  âŸ¦mulâŸ§R .rel-mor w (linear-const , xâ‚ , yâ‚) (_ , xâ‚‚ , yâ‚‚) (refl , xâ‚â‚‚ , yâ‚â‚‚) =
    trans (congâ‚‚ _*_ xâ‚â‚‚ yâ‚â‚‚)
      (trans (*-comm (eval-LinExp xâ‚‚ (w .env)) yâ‚‚) (eval-âŠ› yâ‚‚ xâ‚‚ (w .env)))

  ------------------------------------------------------------------------------
  -- Booleans and constraints

  data QueryR : âˆ€ w â†’ S.Quant ğ”¹ â†’ Query (w .ctxt) â†’ Set where
    q-constraint : âˆ€ {w b Ï•} â†’
                   eval-Constraint Ï• (w .env) â‰¡ b â†’
                   QueryR w (S.return b) (constraint Ï•)
    q-true       : âˆ€ {w x Ï• Ïˆâ‚ Ïˆâ‚‚} â†’
                   eval-Constraint Ï• (w .env) â‰¡ true â†’
                   QueryR w x Ïˆâ‚ â†’
                   QueryR w x ((constraint Ï• and Ïˆâ‚) or (constraint (negate Ï•) and Ïˆâ‚‚))
    q-false      : âˆ€ {w x Ï• Ïˆâ‚ Ïˆâ‚‚} â†’
                   eval-Constraint Ï• (w .env) â‰¡ false â†’
                   QueryR w x Ïˆâ‚‚ â†’
                   QueryR w x ((constraint Ï• and Ïˆâ‚) or (constraint (negate Ï•) and Ïˆâ‚‚))
    q-ex         : âˆ€ {w k Ï•}   â†’ (âˆ€ q â†’ QueryR (extend-w w q) (k q) Ï•) â†’ QueryR w (S.ex k) (ex Ï•)
    q-ex'        : âˆ€ {w x Ï• Ïˆ} q â†’
                   (âˆ€ q' â†’ (q' â‰¡ q) â‡” True (eval-Constraint Ï• (extend-env (w .env) q'))) â†’
                   QueryR (extend-w w q) x Ïˆ â†’
                   QueryR w x (ex (constraint Ï• and Ïˆ))
    q-and        : âˆ€ {w Ï•â‚ Ï•â‚‚ Ïˆâ‚ Ïˆâ‚‚ } â†’
                   QueryR w Ï•â‚ Ïˆâ‚ â†’
                   QueryR w Ï•â‚‚ Ïˆâ‚‚ â†’
                   QueryR w (Ï•â‚ S.and Ï•â‚‚) (Ïˆâ‚ and Ïˆâ‚‚)
    q-or         : âˆ€ {w Ï•â‚ Ï•â‚‚ Ïˆâ‚ Ïˆâ‚‚ } â†’
                   QueryR w Ï•â‚ Ïˆâ‚ â†’
                   QueryR w Ï•â‚‚ Ïˆâ‚‚ â†’
                   QueryR w (Ï•â‚ S.or Ï•â‚‚) (Ïˆâ‚ or Ïˆâ‚‚)

  ext-Query : âˆ€ {wâ‚ wâ‚‚} (Ï : wâ‚‚ â‡’w wâ‚) xâ‚ xâ‚‚ â†’
              QueryR wâ‚ xâ‚ xâ‚‚ â†’ QueryR wâ‚‚ xâ‚ (rename-Query (Ï .ren) xâ‚‚)
  ext-Query Ï _ _ (q-constraint {Ï• = Ï•} x) =
    q-constraint (trans (sym (ext-evalConstraint Ï• Ï)) x)
  ext-Query Ï _ _ (q-true {Ï• = Ï•} is-true r) rewrite rename-negate (Ï .ren) Ï• =
    q-true (trans (sym (ext-evalConstraint Ï• Ï)) is-true) (ext-Query Ï _ _ r)
  ext-Query Ï _ _ (q-false {Ï• = Ï•} is-false r) rewrite rename-negate (Ï .ren) Ï• =
    q-false (trans (sym (ext-evalConstraint Ï• Ï)) is-false) (ext-Query Ï _ _ r)
  ext-Query Ï _ _ (q-ex r) = q-ex Î» q â†’ ext-Query (under-w Ï) _ _ (r q)
  ext-Query Ï _ _ (q-ex' {Ï• = Ï•} q uniq r) =
    q-ex' q (Î» q' â†’ â‡”-trans (uniq q') (cong-True (ext-evalConstraint Ï• (under-w Ï))))
          (ext-Query (under-w Ï) _ _ r)
  ext-Query Ï _ _ (q-and râ‚ râ‚‚) = q-and (ext-Query Ï _ _ râ‚) (ext-Query Ï _ _ râ‚‚)
  ext-Query Ï _ _ (q-or râ‚ râ‚‚) = q-or (ext-Query Ï _ _ râ‚) (ext-Query Ï _ _ râ‚‚)

  âŸ¦BoolâŸ§R : LinearityVal â†’ PolarityVal â†’ WRel
  âŸ¦BoolâŸ§R l p .Left = ğ’®.âŸ¦BoolâŸ§ l p
  âŸ¦BoolâŸ§R l p .Right = ğ’©.âŸ¦BoolâŸ§ l p
  âŸ¦BoolâŸ§R l U .rel w b Ï• = b â‰¡ eval-Constraint Ï• (w .env)
  âŸ¦BoolâŸ§R l U .ext Ï b Ï• eq = trans eq (ext-evalConstraint Ï• Ï)
  âŸ¦BoolâŸ§R l Ex .rel = QueryR
  âŸ¦BoolâŸ§R l Ex .ext = ext-Query

  âŸ¦â‰¤âŸ§ : âˆ€ {lâ‚ lâ‚‚ lâ‚ƒ} â†’ (FlatR (MaxLinRel lâ‚ lâ‚‚ lâ‚ƒ) âŸ¦Ã—âŸ§R (âŸ¦NumâŸ§R lâ‚ âŸ¦Ã—âŸ§R âŸ¦NumâŸ§R lâ‚‚)) ===> âŸ¦BoolâŸ§R lâ‚ƒ U
  âŸ¦â‰¤âŸ§ .left = ğ’®.âŸ¦â‰¤âŸ§
  âŸ¦â‰¤âŸ§ .right = ğ’©.âŸ¦â‰¤âŸ§
  âŸ¦â‰¤âŸ§ .rel-mor w (const-const   , xâ‚ , yâ‚) (_ , xâ‚‚ , yâ‚‚) (refl , xâ‚â‚‚ , yâ‚â‚‚) =
    congâ‚‚ _â‰¤áµ‡_ xâ‚â‚‚ yâ‚â‚‚
  âŸ¦â‰¤âŸ§ .rel-mor w (const-linear  , xâ‚ , yâ‚) (_ , xâ‚‚ , yâ‚‚) (refl , xâ‚â‚‚ , yâ‚â‚‚) =
    congâ‚‚ _â‰¤áµ‡_ xâ‚â‚‚ yâ‚â‚‚
  âŸ¦â‰¤âŸ§ .rel-mor w (linear-const  , xâ‚ , yâ‚) (_ , xâ‚‚ , yâ‚‚) (refl , xâ‚â‚‚ , yâ‚â‚‚) =
    congâ‚‚ _â‰¤áµ‡_ xâ‚â‚‚ yâ‚â‚‚
  âŸ¦â‰¤âŸ§ .rel-mor w (linear-linear , xâ‚ , yâ‚) (_ , xâ‚‚ , yâ‚‚) (refl , xâ‚â‚‚ , yâ‚â‚‚) =
    congâ‚‚ _â‰¤áµ‡_ xâ‚â‚‚ yâ‚â‚‚

  âŸ¦andâŸ§ : âˆ€ {lâ‚ lâ‚‚ lâ‚ƒ pâ‚ pâ‚‚ pâ‚ƒ} â†’
            (FlatR (MaxLinRel lâ‚ lâ‚‚ lâ‚ƒ) âŸ¦Ã—âŸ§R
             (FlatR (MaxPolRel pâ‚ pâ‚‚ pâ‚ƒ) âŸ¦Ã—âŸ§R
              (âŸ¦BoolâŸ§R lâ‚ pâ‚ âŸ¦Ã—âŸ§R âŸ¦BoolâŸ§R lâ‚‚ pâ‚‚))) ===> âŸ¦BoolâŸ§R lâ‚ƒ pâ‚ƒ
  âŸ¦andâŸ§ .left = ğ’®.âŸ¦andâŸ§
  âŸ¦andâŸ§ .right = ğ’©.âŸ¦andâŸ§
  âŸ¦andâŸ§ .rel-mor w (p , U-U , xâ‚ , yâ‚) (_ , _ , xâ‚‚ , yâ‚‚) (refl , refl , xâ‚â‚‚ , yâ‚â‚‚) =
    congâ‚‚ _âˆ§_ xâ‚â‚‚ yâ‚â‚‚
  âŸ¦andâŸ§ .rel-mor w (p , U-Ex , xâ‚ , yâ‚) (_ , _ , xâ‚‚ , yâ‚‚) (refl , refl , xâ‚â‚‚ , yâ‚â‚‚) =
    q-and (q-constraint (sym xâ‚â‚‚)) yâ‚â‚‚
  âŸ¦andâŸ§ .rel-mor w (p , Ex-U , xâ‚ , yâ‚) (_ , _ , xâ‚‚ , yâ‚‚) (refl , refl , xâ‚â‚‚ , yâ‚â‚‚) =
    q-and xâ‚â‚‚ (q-constraint (sym yâ‚â‚‚))
  âŸ¦andâŸ§ .rel-mor w (p , Ex-Ex , xâ‚ , yâ‚) (_ , _ , xâ‚‚ , yâ‚‚) (refl , refl , xâ‚â‚‚ , yâ‚â‚‚) =
    q-and xâ‚â‚‚ yâ‚â‚‚

  âŸ¦orâŸ§ : âˆ€ {lâ‚ lâ‚‚ lâ‚ƒ pâ‚ pâ‚‚ pâ‚ƒ} â†’
            (FlatR (MaxLinRel lâ‚ lâ‚‚ lâ‚ƒ) âŸ¦Ã—âŸ§R
             (FlatR (MaxPolRel pâ‚ pâ‚‚ pâ‚ƒ) âŸ¦Ã—âŸ§R
              (âŸ¦BoolâŸ§R lâ‚ pâ‚ âŸ¦Ã—âŸ§R âŸ¦BoolâŸ§R lâ‚‚ pâ‚‚))) ===> âŸ¦BoolâŸ§R lâ‚ƒ pâ‚ƒ
  âŸ¦orâŸ§ .left = ğ’®.âŸ¦orâŸ§
  âŸ¦orâŸ§ .right = ğ’©.âŸ¦orâŸ§
  âŸ¦orâŸ§ .rel-mor w (p , U-U , xâ‚ , yâ‚) (_ , _ , xâ‚‚ , yâ‚‚) (refl , refl , xâ‚â‚‚ , yâ‚â‚‚) =
    congâ‚‚ _âˆ¨_ xâ‚â‚‚ yâ‚â‚‚
  âŸ¦orâŸ§ .rel-mor w (p , U-Ex , xâ‚ , yâ‚) (_ , _ , xâ‚‚ , yâ‚‚) (refl , refl , xâ‚â‚‚ , yâ‚â‚‚) =
    q-or (q-constraint (sym xâ‚â‚‚)) yâ‚â‚‚
  âŸ¦orâŸ§ .rel-mor w (p , Ex-U , xâ‚ , yâ‚) (_ , _ , xâ‚‚ , yâ‚‚) (refl , refl , xâ‚â‚‚ , yâ‚â‚‚) =
    q-or xâ‚â‚‚ (q-constraint (sym yâ‚â‚‚))
  âŸ¦orâŸ§ .rel-mor w (p , Ex-Ex , xâ‚ , yâ‚) (_ , _ , xâ‚‚ , yâ‚‚) (refl , refl , xâ‚â‚‚ , yâ‚â‚‚) =
    q-or xâ‚â‚‚ yâ‚â‚‚

  âŸ¦notâŸ§ : âˆ€ {l pâ‚ pâ‚‚} â†’ (FlatR (NegPolRel pâ‚ pâ‚‚) âŸ¦Ã—âŸ§R âŸ¦BoolâŸ§R l pâ‚) ===> âŸ¦BoolâŸ§R l pâ‚‚
  âŸ¦notâŸ§ .left = ğ’®.âŸ¦notâŸ§
  âŸ¦notâŸ§ {l} .right = ğ’©.âŸ¦notâŸ§ {l = l}
  âŸ¦notâŸ§ .rel-mor w (U , xâ‚) (_ , xâ‚‚) (refl , xâ‚â‚‚) =
    trans (cong not xâ‚â‚‚) (eval-negate xâ‚‚ (w .env))

  ------------------------------------------------------------------------------
  module _ (X : WRel) where

    LetLiftR : (w : World) â†’ X .Left â†’ N.LetLift (X .Right .N.Carrier) (w .ctxt) â†’ Set
    LetLiftR w a (N.return b) = X .rel w a b
    LetLiftR w a (N.if c kâ‚ kâ‚‚) =
      ifáµ‡ (eval-Constraint c (w .env))
       then LetLiftR w a kâ‚
       else LetLiftR w a kâ‚‚
    LetLiftR w a (N.let-linexp e k) =
      LetLiftR (extend-w w (eval-LinExp e (w .env))) a k
    LetLiftR w a (N.let-funexp x k) =
      LetLiftR (extend-w w (extFunc (w .env x))) a k

    ext-lift : âˆ€ {wâ‚ wâ‚‚} (Ï : wâ‚‚ â‡’w wâ‚) la lb â†’
               LetLiftR wâ‚ la lb â†’
               LetLiftR wâ‚‚ la (N.rename-lift (X .Right .N.rename) (Ï .ren) lb)
    ext-lift Ï a (N.return b) = X .ext Ï a b
    ext-lift {wâ‚} Ï a (N.if c tru fal) rewrite sym (ext-evalConstraint c Ï) with eval-Constraint c (wâ‚ .env)
    ... | false = ext-lift Ï a fal
    ... | true  = ext-lift Ï a tru
    ext-lift Ï a (N.let-linexp x lb) =
      ext-lift (under-w' (sym (ext-evalLinExp x Ï)) Ï) a lb
    ext-lift Ï a (N.let-funexp x lb) =
      ext-lift (under-w' (cong extFunc (Ï .presv x)) Ï) a lb

    LiftMR : WRel
    LiftMR .Left = ğ’®.Mon (X .Left)
    LiftMR .Right = ğ’©.Mon (X .Right)
    LiftMR .rel = LetLiftR
    LiftMR .ext = ext-lift
{-
  eval-Lift : âˆ€ {Î”} â†’ N.LetLift (ğ’©.âŸ¦BoolâŸ§ constraint .N.Carrier) Î” â†’ Env Î” â†’ ğ”¹
  eval-Lift (return x) Î· = eval-Constraint extFunc x Î·
  eval-Lift (if x xâ‚ xâ‚‚) Î· = ifáµ‡ (eval-Constraint extFunc x Î·) then eval-Lift xâ‚ Î· else eval-Lift xâ‚‚ Î·
  eval-Lift (let-linexp x xâ‚) Î· = eval-Lift xâ‚ (extend-env Î· (eval-LinExp x Î·))
  eval-Lift (let-funexp x xâ‚) Î· = eval-Lift xâ‚ (extend-env Î· (extFunc (Î· x)))

  lift-correct : âˆ€ {w b} e â†’ LetLiftR Constraint-WRel w b e â†’ b â‰¡ eval-Lift e (w .env)
  lift-correct (return x) r = r
  lift-correct {w} (if x e eâ‚) r with eval-Constraint extFunc x (w .env)
  ... | true = lift-correct e r
  ... | false = lift-correct eâ‚ r
  lift-correct (let-linexp x e) r = lift-correct e r
  lift-correct (let-funexp x e) r = lift-correct e r
-}
  let-bindR : âˆ€ {X Y} w x y â†’
    (f : X .Left â†’ Y .Left)
    (g : (X .Right .N.Carrier â‡’â‚– N.LetLift (Y .Right .N.Carrier)) (w .ctxt)) â†’
    LetLiftR X w x y â†’
    (âˆ€ w' (Ï : w' â‡’w w) a b â†’ X .rel w' a b â†’ LetLiftR Y w' (f a) (g (w' .ctxt) (Ï .ren) b)) â†’
    LetLiftR Y w (f x) (N.bind-let y g)
  let-bindR w xâ‚ (N.return xâ‚‚) f g r-xâ‚xâ‚‚ r-fg = r-fg w id-w xâ‚ xâ‚‚ r-xâ‚xâ‚‚
  let-bindR w xâ‚ (N.if c xâ‚‚â‚ xâ‚‚â‚‚) f g r-xâ‚xâ‚‚ r-fg with eval-Constraint c (w .env)
  ... | true = let-bindR w xâ‚ xâ‚‚â‚ f g r-xâ‚xâ‚‚ r-fg
  ... | false = let-bindR w xâ‚ xâ‚‚â‚‚ f g r-xâ‚xâ‚‚ r-fg
  let-bindR w xâ‚ (N.let-linexp e xâ‚‚) f g r-xâ‚xâ‚‚ r-fg =
    let-bindR (extend-w w (eval-LinExp e (w .env)))
       xâ‚ xâ‚‚ f (Î» Î”' Ï â†’ g Î”' (wk-r âˆ˜ Ï))
       r-xâ‚xâ‚‚
       Î» w' Ï â†’ r-fg w' (wk-w âˆ˜w Ï)
  let-bindR w xâ‚ (N.let-funexp v xâ‚‚) f g r-xâ‚xâ‚‚ r-fg =
    let-bindR (extend-w w (extFunc (w .env v)))
       xâ‚ xâ‚‚ f (Î» Î”' Ï â†’ g Î”' (wk-r âˆ˜ Ï))
       r-xâ‚xâ‚‚
       Î» w' Ï â†’ r-fg w' (wk-w âˆ˜w Ï)

  âŸ¦returnâŸ§R : âˆ€ {X} â†’ X ===> LiftMR X
  âŸ¦returnâŸ§R .left = ğ’®.âŸ¦returnâŸ§
  âŸ¦returnâŸ§R .right = ğ’©.âŸ¦returnâŸ§
  âŸ¦returnâŸ§R .rel-mor w xâ‚ xâ‚‚ r-xâ‚xâ‚‚ = r-xâ‚xâ‚‚

  âŸ¦extFuncâŸ§R : âŸ¦NumâŸ§R linear ===> LiftMR (âŸ¦NumâŸ§R linear)
  âŸ¦extFuncâŸ§R .left = ğ’®.âŸ¦extFuncâŸ§
  âŸ¦extFuncâŸ§R .right = ğ’©.âŸ¦extFuncâŸ§
  âŸ¦extFuncâŸ§R .rel-mor w xâ‚ xâ‚‚ r-xâ‚xâ‚‚ =
    trans (cong extFunc r-xâ‚xâ‚‚) (sym (*-identityË¡ _))

  âŸ¦ifâŸ§R : âˆ€ {X} â†’ ((LiftMR X âŸ¦Ã—âŸ§R LiftMR X) âŸ¦Ã—âŸ§R âŸ¦BoolâŸ§R linear U) ===> LiftMR X
  âŸ¦ifâŸ§R .left = ğ’®.âŸ¦ifâŸ§
  âŸ¦ifâŸ§R .right = ğ’©.âŸ¦ifâŸ§
  âŸ¦ifâŸ§R .rel-mor w ((trâ‚ , faâ‚) , false) ((trâ‚‚ , faâ‚‚) , Ï•) ((trâ‚-trâ‚‚ , faâ‚-faâ‚‚) , eq) rewrite sym eq = faâ‚-faâ‚‚
  âŸ¦ifâŸ§R .rel-mor w ((trâ‚ , faâ‚) , true) ((trâ‚‚ , faâ‚‚) , Ï•) ((trâ‚-trâ‚‚ , faâ‚-faâ‚‚) , eq) rewrite sym eq = trâ‚-trâ‚‚

  extendR : âˆ€ {X Y Z} â†’ ((X âŸ¦Ã—âŸ§R Y) ===> LiftMR Z) â†’ (X âŸ¦Ã—âŸ§R LiftMR Y) ===> LiftMR Z
  extendR f .left = ğ’®.âŸ¦extendâŸ§ (f .left)
  extendR f .right = ğ’©.âŸ¦extendâŸ§ (f .right)
  extendR {X} f .rel-mor w (xâ‚ , lyâ‚) (xâ‚‚ , lyâ‚‚) (xâ‚xâ‚‚ , lyâ‚-lyâ‚‚) =
    let-bindR w lyâ‚ lyâ‚‚
      (Î» y â†’ f .left (xâ‚ , y))
      (Î» Î”' Ï y â†’ f .right .N.mor (X .Right .N.rename Ï xâ‚‚ , y))
      lyâ‚-lyâ‚‚
      Î» w' Ï yâ‚ yâ‚‚ yâ‚yâ‚‚ â†’
        f .rel-mor w' (xâ‚ , yâ‚) (X .Right .N.rename (Ï .ren) xâ‚‚ , yâ‚‚) (X .ext Ï xâ‚ xâ‚‚ xâ‚xâ‚‚ , yâ‚yâ‚‚)

  compile-lemma : âˆ€ l w xâ‚ xâ‚‚ â†’ LetLiftR (âŸ¦BoolâŸ§R l Ex) w xâ‚ xâ‚‚ â†’ QueryR w xâ‚ (N.compile xâ‚‚)
  compile-lemma l w xâ‚ (N.return x) r = r
  compile-lemma l w xâ‚ (N.if Ï• tr fa) r with is-true-or-false (eval-Constraint Ï• (w .env))
  ... | injâ‚ is-true =
         q-true is-true (compile-lemma l w _ tr (subst (Î» â–¡ â†’ ifáµ‡ â–¡ then _ else _) is-true r))
  ... | injâ‚‚ is-false =
         q-false is-false (compile-lemma l w _ fa (subst (Î» â–¡ â†’ ifáµ‡ â–¡ then _ else _) is-false r))
  compile-lemma l w xâ‚ (N.let-linexp e k) r =
    q-ex' q
          (Î» q' â†’
            â‡”-trans (eq-cong
                      (sym (*-identityË¡ q'))
                      (ext-evalLinExp e wk-w))
             (â‡”-sym (does-cong (1â„š * q' â‰Ÿ
                                eval-LinExp (rename-LinExp succ e) (extend-env (w .env) q')))))
          (compile-lemma l (extend-w w q) xâ‚ k r)
    where q : â„š
          q = eval-LinExp e (w .env)
  compile-lemma l w xâ‚ (N.let-funexp x k) r =
    q-ex' q
          (Î» q' â†’ â‡”-sym (does-cong (q' â‰Ÿ extFunc (w .env x))))
          (compile-lemma l (extend-w w q) xâ‚ k r)
    where q : â„š
          q = extFunc (w .env x)

  âŸ¦âˆƒâŸ§ : âˆ€ {pâ‚ pâ‚‚ l} â†’
       (FlatR (QuantifyRel pâ‚ pâ‚‚) âŸ¦Ã—âŸ§R (âŸ¦NumâŸ§R linear âŸ¦â‡’âŸ§R LiftMR (âŸ¦BoolâŸ§R l pâ‚))) ===> âŸ¦BoolâŸ§R l pâ‚‚
  âŸ¦âˆƒâŸ§ .left = ğ’®.âŸ¦âˆƒâŸ§
  âŸ¦âˆƒâŸ§ {l = l} .right = ğ’©.âŸ¦âˆƒâŸ§ {l = l}
  âŸ¦âˆƒâŸ§ {l = l} .rel-mor w (U  , fâ‚) (_ , fâ‚‚) (refl , r) =
    q-ex (Î» q â†’ compile-lemma l (extend-w w q)
                     (S.return (fâ‚ q))
                     (N.bind-let (fâ‚‚ (w .ctxt ,âˆ™) succ (var 1â„š zero))
                       (Î» Î”' Ï Ï• â†’ N.return (constraint Ï•)))
                     (let-bindR (extend-w w q)
                       (fâ‚ q)
                       (fâ‚‚ (w .ctxt ,âˆ™) succ (var 1â„š zero))
                       S.return
                       _
                       (r (extend-w w q) wk-w q (var 1â„š zero) (sym (*-identityË¡ q)))
                       Î» w' Ï a b x â†’ q-constraint (sym x)))
  âŸ¦âˆƒâŸ§ {l = l} .rel-mor w (Ex , fâ‚) (_ , fâ‚‚) (refl , r) =
    q-ex Î» q â†’ compile-lemma l (extend-w w q) (fâ‚ q) (fâ‚‚ (w .ctxt ,âˆ™) succ (var 1â„š zero))
                 (r (extend-w w q) wk-w q (var 1â„š zero) (sym (*-identityË¡ q)))

  â„³ : Model (suc 0â„“) 0â„“
  â„³ .Model.âŸ¦TypeâŸ§ = WRel
  â„³ .Model._==>_ = _===>_
  â„³ .Model.Flat = FlatR
  â„³ .Model.âŸ¦idâŸ§ = âŸ¦idâŸ§R
  â„³ .Model._âˆ˜_ = _âˆ˜R_
  â„³ .Model._âŸ¦Ã—âŸ§_ = _âŸ¦Ã—âŸ§R_
  â„³ .Model.âŸ¦âŠ¤âŸ§ = âŸ¦âŠ¤âŸ§R
  â„³ .Model.âŸ¦terminalâŸ§ = âŸ¦terminalâŸ§R
  â„³ .Model.âŸ¦projâ‚âŸ§ = âŸ¦projâ‚âŸ§R
  â„³ .Model.âŸ¦projâ‚‚âŸ§ = âŸ¦projâ‚‚âŸ§R
  â„³ .Model.âŸ¨_,_âŸ© = âŸ¨_,_âŸ©R
  â„³ .Model._âŸ¦â‡’âŸ§_ = _âŸ¦â‡’âŸ§R_
  â„³ .Model.âŸ¦Î›âŸ§ = âŸ¦Î›âŸ§R
  â„³ .Model.âŸ¦evalâŸ§ = âŸ¦evalâŸ§R
  â„³ .Model.âŸ¦âˆ€âŸ§ = âŸ¦âˆ€âŸ§R
  â„³ .Model.âŸ¦âˆ€-introâŸ§ = âŸ¦âˆ€-introâŸ§R
  â„³ .Model.âŸ¦âˆ€-elimâŸ§ = âŸ¦âˆ€-elimâŸ§R
  â„³ .Model.Mon = LiftMR
  â„³ .Model.âŸ¦returnâŸ§ = âŸ¦returnâŸ§R
  â„³ .Model.âŸ¦extendâŸ§ = extendR
  â„³ .Model.âŸ¦NumâŸ§ = âŸ¦NumâŸ§R
  â„³ .Model.âŸ¦addâŸ§ = âŸ¦addâŸ§R
  â„³ .Model.âŸ¦mulâŸ§ = âŸ¦mulâŸ§R
  â„³ .Model.âŸ¦constâŸ§ = âŸ¦constâŸ§R
  â„³ .Model.âŸ¦extFuncâŸ§ = âŸ¦extFuncâŸ§R
  â„³ .Model.âŸ¦BoolâŸ§ = âŸ¦BoolâŸ§R
  â„³ .Model.âŸ¦notâŸ§ = âŸ¦notâŸ§
  â„³ .Model.âŸ¦andâŸ§ = âŸ¦andâŸ§
  â„³ .Model.âŸ¦orâŸ§ = âŸ¦orâŸ§
  â„³ .Model.âŸ¦â‰¤âŸ§ = âŸ¦â‰¤âŸ§
  â„³ .Model.âŸ¦ifâŸ§ = âŸ¦ifâŸ§R
  â„³ .Model.âŸ¦IndexâŸ§ = âŸ¦IndexâŸ§R
  â„³ .Model.âŸ¦idxâŸ§ n i .left = ğ’®.âŸ¦idxâŸ§ n i
  â„³ .Model.âŸ¦idxâŸ§ n i .right = ğ’©.âŸ¦idxâŸ§ n i
  â„³ .Model.âŸ¦idxâŸ§ n i .rel-mor w _ _ _ = refl
  â„³ .Model.âŸ¦âˆƒâŸ§ = âŸ¦âˆƒâŸ§

  module â„ = Interpret â„³

  standard : Îµ / Îµ âŠ¢ Bool linear Ex â†’ Set
  standard t = S.eval-Quant (â„.âŸ¦ t âŸ§tm (lift tt) .left tt) True

  normalise : Îµ / Îµ âŠ¢ Bool linear Ex â†’ FlatQuery Îµ
  normalise t = flatten (N.compile (â„.âŸ¦ t âŸ§tm (lift tt) .right .N.mor tt))

  QueryR-ok : âˆ€ w {xâ‚ xâ‚‚} â†’
                QueryR w xâ‚ xâ‚‚ â†’
                S.eval-Quant xâ‚ True â‡” eval-Query xâ‚‚ (w .env)
  QueryR-ok w (q-constraint x) = cong-True (sym x)
  QueryR-ok w (q-true {Ï• = Ï•} is-true r) =
    â‡”-trans (QueryR-ok w r)
    (â‡”-trans or-left
            (âŠ-cong (â‡”-trans âŠ¤-fst (Ã—-cong (âŠ¤-bool is-true) â‡”-refl))
                    (â‡”-trans âŠ¥-fst (Ã—-cong (âŠ¥-bool (trans (sym (eval-negate Ï• (w .env))) (cong not is-true)))
                                           â‡”-refl))))
  QueryR-ok w (q-false {Ï• = Ï•} is-false r) =
    â‡”-trans (QueryR-ok w r)
    (â‡”-trans or-right
    (âŠ-cong
      (â‡”-trans âŠ¥-fst (Ã—-cong
                       (âŠ¥-bool is-false)
                       â‡”-refl))
      (â‡”-trans âŠ¤-fst (Ã—-cong
                       (âŠ¤-bool (trans (sym (eval-negate Ï• (w .env))) (cong not is-false)))
                       â‡”-refl))))
  QueryR-ok w (q-ex x) = cong-âˆƒ (Î» q â†’ QueryR-ok (extend-w w q) (x q))
  QueryR-ok w (q-ex' q x r) =
    â‡”-trans (QueryR-ok (extend-w w q) r)
             (â‡”-trans (known q) (cong-âˆƒ (Î» q' â†’ Ã—-cong (x q') â‡”-refl)))
  QueryR-ok w (q-and râ‚ râ‚‚) = Ã—-cong (QueryR-ok w râ‚) (QueryR-ok w râ‚‚)
  QueryR-ok w (q-or râ‚ râ‚‚) = âŠ-cong (QueryR-ok w râ‚) (QueryR-ok w râ‚‚)

  ext-FlatQuery : âˆ€ {wâ‚ wâ‚‚} (Ï : wâ‚‚ â‡’w wâ‚) Ï• â†’
                  eval-FlatQuery Ï• (wâ‚ .env) â‡”
                     eval-FlatQuery (rename-FlatQuery (Ï .ren) Ï•) (wâ‚‚ .env)
  ext-FlatQuery Ï (constraint Ï•) = cong-True (ext-evalConstraint Ï• Ï)
  ext-FlatQuery Ï (ex Ï•) = cong-âˆƒ Î» q â†’ ext-FlatQuery (under-w Ï) Ï•

  equi-conj-constraint : âˆ€ {Î”} (Ï• : Constraint Î”) Ïˆ Î· â†’
                         (True (eval-Constraint Ï• Î·) Ã— eval-FlatQuery Ïˆ Î·)
                            â‡” eval-FlatQuery (conj-constraint Ï• Ïˆ) Î·
  equi-conj-constraint Ï• (constraint x) Î· = True-âˆ§
  equi-conj-constraint Ï• (ex Ïˆ) Î· =
    â‡”-trans
      and-comm-left
      (â‡”-trans
       (cong-âˆƒ Î» q â†’ Ã—-cong (cong-True (ext-evalConstraint Ï• wk-w)) â‡”-refl)
       (cong-âˆƒ Î» q â†’
          equi-conj-constraint (rename-Constraint succ Ï•) Ïˆ (extend-env Î· q)))

  equi-conj : âˆ€ {Î”} (Ï• : FlatQuery Î”) Ïˆ Î· â†’
              (eval-FlatQuery Ï• Î· Ã— eval-FlatQuery Ïˆ Î·) â‡” eval-FlatQuery (conj Ï• Ïˆ) Î·
  equi-conj (constraint Ï•) Ïˆ Î· = equi-conj-constraint Ï• Ïˆ Î·
  equi-conj (ex Ï•) Ïˆ Î· =
    â‡”-trans
     and-comm-right
     (cong-âˆƒ Î» q â†’
      â‡”-trans
       (Ã—-cong â‡”-refl (ext-FlatQuery wk-w Ïˆ))
       (equi-conj Ï• (rename-FlatQuery succ Ïˆ) (extend-env Î· q)))

  equi-disj-constraint : âˆ€ {Î”} (Ï• : Constraint Î”) Ïˆ Î· â†’
                         (True (eval-Constraint Ï• Î·) âŠ eval-FlatQuery Ïˆ Î·)
                            â‡” eval-FlatQuery (disj-constraint Ï• Ïˆ) Î·
  equi-disj-constraint Ï• (constraint x) Î· = True-âˆ¨
  equi-disj-constraint Ï• (ex Ïˆ) Î· =
    â‡”-trans
     (or-comm-right 1â„š)
     (cong-âˆƒ
      Î» q â†’ â‡”-trans
             (âŠ-cong (cong-True (ext-evalConstraint Ï• wk-w)) â‡”-refl)
             (equi-disj-constraint (rename-Constraint succ Ï•) Ïˆ (extend-env Î· q)))

  equi-disj : âˆ€ {Î”} (Ï• : FlatQuery Î”) Ïˆ Î· â†’
              (eval-FlatQuery Ï• Î· âŠ eval-FlatQuery Ïˆ Î·) â‡” eval-FlatQuery (disj Ï• Ïˆ) Î·
  equi-disj (constraint Ï•) Ïˆ Î· = equi-disj-constraint Ï• Ïˆ Î·
  equi-disj (ex Ï•) Ïˆ Î· =
    â‡”-trans (or-comm-left 1â„š)
     (cong-âˆƒ Î» q â†’
      â‡”-trans
       (âŠ-cong â‡”-refl (ext-FlatQuery wk-w Ïˆ))
       (equi-disj Ï• (rename-FlatQuery succ Ïˆ) (extend-env Î· q)))

  flatten-ok : âˆ€ {Î”} (Ï• : Query Î”) Î· â†’
               eval-Query Ï• Î· â‡” eval-FlatQuery (flatten Ï•) Î·
  flatten-ok (constraint x) Î· = â‡”-refl
  flatten-ok (ex Ï•) Î· = cong-âˆƒ Î» q â†’ flatten-ok Ï• (extend-env Î· q)
  flatten-ok (Ï• and Ïˆ) Î· =
    â‡”-trans (Ã—-cong (flatten-ok Ï• Î·) (flatten-ok Ïˆ Î·))
              (equi-conj (flatten Ï•) (flatten Ïˆ) Î·)
  flatten-ok (Ï• or Ïˆ) Î· =
    â‡”-trans (âŠ-cong (flatten-ok Ï• Î·) (flatten-ok Ïˆ Î·))
              (equi-disj (flatten Ï•) (flatten Ïˆ) Î·)

  full-correctness : (t : Îµ / Îµ âŠ¢ Bool linear Ex) â†’
                     standard t â‡” eval-FlatQuery (normalise t) (empty .env)
  full-correctness t =
    â‡”-trans
      (QueryR-ok empty (compile-lemma linear empty _ q (â„.âŸ¦ t âŸ§tm (lift tt) .rel-mor empty tt tt tt)))
      (flatten-ok (N.compile q) empty-env)
    where q : N.LetLift Query Îµ
          q = â„.âŸ¦ t âŸ§tm (lift tt) .right .N.mor tt
